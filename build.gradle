buildscript {
    repositories {
        jcenter()
        maven {
            url 'https://dl.bintray.com/palantir/releases/'
        }
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }

    dependencies {
        classpath 'com.netflix.nebula:nebula-publishing-plugin:13.6.1'
        classpath 'com.netflix.nebula:gradle-info-plugin:5.1.0'
        classpath 'com.palantir.gradle.consistentversions:gradle-consistent-versions:1.12.4'
        classpath 'com.palantir.baseline:gradle-baseline-java:2.9.2'
        classpath 'com.palantir.gradle.gitversion:gradle-git-version:0.12.2'
        classpath 'gradle.plugin.org.inferred:gradle-processors:3.1.0'
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4"
    }
}

plugins {
    id 'com.gradle.plugin-publish' version '0.10.1'
}

repositories {
    jcenter()
    maven {
        url 'https://dl.bintray.com/palantir/releases/'
    }
}


apply plugin: 'java-gradle-plugin'
apply plugin: 'groovy'
apply plugin: 'com.palantir.baseline'
apply plugin: 'com.palantir.consistent-versions'
apply plugin: 'com.palantir.git-version'
apply plugin: 'org.inferred.processors'
apply from: "$rootDir/gradle/publish-jar.gradle"

group 'com.palantir.gradle.consistentversions'
version gitVersion()

sourceCompatibility = 1.8

dependencies {
    implementation 'com.google.guava:guava'
    implementation 'com.netflix.nebula:nebula-dependency-recommender'

    testCompile platform('org.junit:junit-bom')
    testImplementation 'com.netflix.nebula:nebula-test'
    testImplementation 'com.fasterxml.jackson.core:jackson-databind'
    testImplementation "org.junit.jupiter:junit-jupiter-api"
    testRuntimeOnly "org.junit.vintage:junit-vintage-engine"

    annotationProcessor "org.immutables:value"
    compileOnly "org.immutables:value::annotations"
}

tasks.withType(Test) {
    useJUnitPlatform()

    systemProperties = [
            'junit.platform.output.capture.stdout'                  : 'true',
            'junit.platform.output.capture.stderr'                  : 'true',
            'junit.jupiter.execution.parallel.enabled'              : 'true',
            'junit.jupiter.execution.parallel.config.strategy'      : 'dynamic',
            'junit.jupiter.execution.parallel.config.dynamic.factor': '1'
    ]
}

tasks.withType(JavaCompile) {
    // necessary until https://github.com/palantir/gradle-baseline/issues/389
    options.errorprone.errorproneArgs += ['-Xep:Slf4jLogsafeArgs:OFF', '-Xep:PreferSafeLoggableExceptions:OFF']
    options.compilerArgs += ['-Werror']
}

gradlePlugin {
    plugins {
        consistentVersions {
            id = 'com.palantir.consistent-versions'
            implementationClass = 'com.palantir.gradle.versions.ConsistentVersionsPlugin'
        }
        versionsLock {
            id = 'com.palantir.versions-lock'
            implementationClass = 'com.palantir.gradle.versions.VersionsLockPlugin'
        }
        versionsProps {
            id = 'com.palantir.versions-props'
            implementationClass = 'com.palantir.gradle.versions.VersionsPropsPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/palantir/gradle-consistent-versions'
    vcsUrl = 'https://github.com/palantir/gradle-consistent-versions'
    description = 'Compact, constraint-friendly lockfiles for your dependencies'
    tags = ['versions']

    // Don't publish the other plugins to the plugin portal
    plugins.removeIf { it.name != 'consistentVersions' }

    plugins {
        consistentVersions {
            displayName = 'Compact, constraint-friendly lockfiles for your dependencies'
        }
    }
}

tasks.publish.dependsOn tasks.publishPlugins
publishPlugins.onlyIf { versionDetails().isCleanTag }
project.ext.'gradle.publish.key' = System.env["GRADLE_KEY"]
project.ext.'gradle.publish.secret' = System.env["GRADLE_SECRET"]
