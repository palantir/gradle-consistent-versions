buildscript {
    repositories {
        mavenCentral() { metadataSources { mavenPom(); ignoreGradleMetadataRedirection() } }
        gradlePluginPortal() { metadataSources { mavenPom(); ignoreGradleMetadataRedirection() } }
    }

    dependencies {
        classpath 'com.palantir.jakartapackagealignment:jakarta-package-alignment:0.6.0'
        classpath 'com.palantir.gradle.jdks:gradle-jdks:0.51.0'
        classpath 'com.palantir.gradle.jdkslatest:gradle-jdks-latest:0.16.0'
        classpath 'com.palantir.gradle.externalpublish:gradle-external-publish-plugin:1.19.0'
        classpath 'com.palantir.gradle.failure-reports:gradle-failure-reports:1.13.0'
        classpath 'com.palantir.javaformat:gradle-palantir-java-format:2.50.0'
        classpath 'com.palantir.gradle.consistentversions:gradle-consistent-versions:2.27.0'
        classpath 'com.palantir.baseline:gradle-baseline-java:5.72.0'
        classpath 'com.palantir.gradle.gitversion:gradle-git-version:3.1.0'
        classpath 'gradle.plugin.org.inferred:gradle-processors:3.7.0'
    }
}

plugins {
 id 'com.gradle.plugin-publish' version '1.3.0'
}

apply plugin: 'com.palantir.external-publish'
apply plugin: 'java-gradle-plugin'
apply plugin: 'groovy'
apply plugin: 'com.palantir.baseline'
apply plugin: 'com.palantir.consistent-versions'
apply plugin: 'com.palantir.git-version'
apply plugin: 'com.palantir.jdks'
apply plugin: 'org.inferred.processors'
apply plugin: 'com.palantir.external-publish-jar'
apply plugin: 'com.palantir.failure-reports'
apply plugin: 'com.palantir.baseline-java-versions'
apply plugin: 'com.palantir.jdks.latest'

version System.env.CIRCLE_TAG ?: gitVersion()

allprojects {
    apply plugin: 'com.palantir.java-format'
    apply plugin: 'com.palantir.jakarta-package-alignment'

    repositories {
        mavenCentral() { metadataSources { mavenPom(); ignoreGradleMetadataRedirection() } }
    }

    group 'com.palantir.gradle.consistentversions'
    version rootProject.version
}

dependencies {
    implementation 'com.google.guava:guava'
    implementation 'com.netflix.nebula:nebula-dependency-recommender'
    implementation 'com.palantir.gradle.failure-reports:gradle-failure-reports-exceptions'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-guava'
    implementation 'one.util:streamex'

    testImplementation platform('org.junit:junit-bom')
    testImplementation 'com.netflix.nebula:nebula-test'
    testImplementation 'org.mockito:mockito-core'
    testImplementation 'com.fasterxml.jackson.core:jackson-databind'
    testImplementation "org.junit.jupiter:junit-jupiter-api"
    testImplementation 'org.assertj:assertj-core'
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly "org.junit.vintage:junit-vintage-engine"

    annotationProcessor "org.immutables:value"
    compileOnly "org.immutables:value::annotations"
}

tasks.withType(Test) {
    useJUnitPlatform()

    systemProperties = [
            'junit.platform.output.capture.stdout'                  : 'true',
            'junit.platform.output.capture.stderr'                  : 'true',
            'junit.jupiter.execution.parallel.enabled'              : 'true',
            'junit.jupiter.execution.parallel.config.strategy'      : 'dynamic',
            'junit.jupiter.execution.parallel.config.dynamic.factor': '1',
            // Ignore Gradle deprecations in tests. Note that we have to tackle those for the Gradle 8 update
            'ignoreDeprecations'                                    : 'true'
    ]
}

tasks.withType(JavaCompile) {
    // necessary until https://github.com/palantir/gradle-baseline/issues/389
    options.compilerArgs += ['-Werror']
    options.errorprone.disable 'Slf4jLogsafeArgs', 'PreferSafeLoggableExceptions'
}

gradlePlugin {
    website = 'https://github.com/palantir/gradle-consistent-versions'
    vcsUrl = 'https://github.com/palantir/gradle-consistent-versions'

    plugins {
        consistentVersions {
            id = 'com.palantir.consistent-versions'
            implementationClass = 'com.palantir.gradle.versions.ConsistentVersionsPlugin'
            displayName = 'Compact, constraint-friendly lockfiles for your dependencies'
            description = displayName
            tags.set(['versions'])
        }
        versionsLock {
            id = 'com.palantir.versions-lock'
            implementationClass = 'com.palantir.gradle.versions.VersionsLockPlugin'
            displayName = 'Compact, constraint-friendly lockfiles for your dependencies'
            description = displayName
            tags.set(['versions'])
        }
        versionsProps {
            id = 'com.palantir.versions-props'
            implementationClass = 'com.palantir.gradle.versions.VersionsPropsPlugin'
            displayName = 'Inject version constraints into every configuration in your projects'
            description = displayName
            tags.set(['versions'])
        }
        versionsPropsIdeaPlugin {
            id = 'com.palantir.version-props-idea'
            implementationClass = 'com.palantir.gradle.versions.VersionsPropsIdeaPlugin'
            displayName = 'Plugin to collect repositories for use by the IDEA plugin.'
            description = displayName
            tags.set(['versions'])
        }
    }
}

afterEvaluate {
    tasks.publishVersionsLockPluginMarkerMavenPublicationToSonatypeRepository.enabled = false
    tasks.publishVersionsPropsPluginMarkerMavenPublicationToSonatypeRepository.enabled = false
}

tasks.publish.dependsOn tasks.publishPlugins
publishPlugins.onlyIf { !System.env.CI || versionDetails().isCleanTag }
project.ext.'gradle.publish.key' = System.env["GRADLE_KEY"]
project.ext.'gradle.publish.secret' = System.env["GRADLE_SECRET"]

javaVersions {
    libraryTarget = 17
}

jdks {
    daemonTarget = 17
}
