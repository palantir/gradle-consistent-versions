apply plugin: 'java'
apply plugin: 'com.palantir.external-publish-intellij'

intellij {
    pluginName = "gradle-consistent-versions"
    updateSinceUntilBuild = true
    version = "2024.1"
    plugins = ['java', 'org.jetbrains.plugins.gradle']
}

patchPluginXml {
    pluginDescription = "Language plugin for version.props allowing for auto-lock updating"
    version = "1.0.0" // For some reason project.version is unspeificed and thus this fails
    sinceBuild = '241' // TODO: test against this version of IntelliJ to ensure no regressions
    untilBuild = ''
}

afterEvaluate {
    tasks.named('test').configure { it.javaLauncher = null }
}

// Javadoc fails if there are no public classes to javadoc, so make it stop complaining.
tasks.named("javadoc", Javadoc) {
    failOnError = false
}

tasks.withType(JavaCompile).configureEach {
    options.errorprone.disable 'PreferSafeLoggableExceptions'
    options.errorprone.disable 'PreferSafeLogger'
    options.errorprone.disable 'StrictUnusedVariable'
}

sourceSets.main.java.srcDirs 'src/main/generated'

javaVersion {
    target = 17
    runtime = 17
}

dependencies {
    implementation 'org.immutables:value'
    testImplementation 'org.junit.jupiter:junit-jupiter'
}